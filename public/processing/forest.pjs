/* @pjs preload="/processing/horseman-with-cape-flipped.png"; */

float timer,delta;

class Tree{
	public float x,y,dy,top,maxTop,trunk,leafshadow,z;
	Tree(float newX,float newY,float newDy,float newZ){
		z = newZ;
		x = newX;;
		top = y = newY - z;
		dy = newDy + ((float)random(9))/100.0;
		maxTop = y - random(200) - 50;
		trunk = random(9)+2;
		leafshadow = random(10);
	}

	void draw(float d){
		strokeWeight(1);
		stroke(51+z,51+z,51+z);
		if(top > maxTop)
			top -= (dy)*d;
		else{
			fill(51+z,51+z,51+z);
			triangle(x-32,top-((top-y)*0.8),x,top-34,x+32,top-((top-y)*0.8));
			fill(12+z,12+z,12+z);
			triangle(x+20-leafshadow,top-((top-y)*0.8),x,top-34,x+32,top-((top-y)*0.8));
		}
		strokeWeight(trunk);
		line(x,y,x,top);
	}
}

class Horseman{
	public float x,y,dx;
	public int cur_frame;
	private PImage sprite,sprite_leftward,sprite_rightward;
	private boolean left;
	private float timer;
	private float savedDx;

	private int animState;
	private final int WALKING = 0;
	private final int RUNNING = 1;
  private final int TURNING = 2;

	private float thewait;

	Horseman(float newX,float newY){
		cur_frame = 0;
		x = newX;
		y = newY;
		dx = -0.04;
		sprite_leftward = loadImage("/processing/horseman-full.png");
		sprite_rightward = loadImage("/processing/horseman-full-flipped.png");
		sprite = sprite_leftward;
		timer = millis();
		left = true;
		savedDx = 0.0;
		thewait = 200;
		
		run();
	}
	void draw(d){
		if(x < 403+55 && animState != TURNING){
			walk();
		}
		else if(x > 1375-55 && animState != TURNING){
			walk();
		}
		else if(animState != TURNING){
			run();
		}
		
		if(x < 403){
			x = 404;
			turn();
		}
		else if(x > 1375){
			x = 1374;
			turn();
		}else{
			x += dx*d;
			if(millis()-timer > thewait){
				if(left){
					cur_frame += 1;
				}else{
					cur_frame -= 1;
				}
				timer = millis();
			}
		}
		
		if(left){
			if(animState == WALKING){
				/* 5,6 */
				if(cur_frame < 5){
					cur_frame = 6;
				}
				if(cur_frame > 6){
					cur_frame = 5;
				}
			}
			else if(animState == RUNNING){
				/* 0,1,2,3 */
				if(cur_frame < 0){
					cur_frame = 3;
				}
				if(cur_frame > 3){
					cur_frame = 0;
				}
			}
			else if(animState == TURNING){
				/* 7,8 */
				if(cur_frame < 7){
					cur_frame = 7;
				}
				if(cur_frame > 8){
					unturn();
				}
			}
		}
		else{
			if(animState == WALKING){
				/* 3,2 */
				if(cur_frame < 2){
					cur_frame = 3;
				}
				if(cur_frame > 3){
					cur_frame = 2;
				}
			}
			else if(animState == RUNNING){
				/* 8,7,6,5 */
				if(cur_frame < 5){
					cur_frame = 8;
				}
				if(cur_frame > 8){
					cur_frame = 5;
				}
			}
			else if(animState == TURNING){
				/* 1,0 */
				if(cur_frame < 0){
					unturn();
				}
				if(cur_frame > 1){
					cur_frame = 1;
				}
			}
		}
		
		image(sprite.get(cur_frame*32,0,32,32),x,y);
	}

	private void walk(){
		dx = dx < 0 ? -0.04 : 0.04;
		animState = WALKING;
	}
	private void run(){
		dx = dx < 0 ? -0.08 : 0.08;
		animState = RUNNING;
	}
	private void turn(){
		savedDx = dx;
		dx = 0.0;
		animState = TURNING;
		y += 2;
	}
	private void unturn(){
		dx = savedDx*-0.1;
		animState = WALKING;
		y -= 2;
		left = !left;
		if(left){
			sprite = sprite_leftward;
			cur_frame = 5;
		}
		else{
			sprite = sprite_rightward;
			cur_frame = 3;
		}
	}
}

Tree trees[];
Horseman horseman;

void setup(){
	size(1500,500);
	background(255);
	frameRate(30);

	trees = new Tree[20];
	for(int i=0;i < trees.length; i++){
		if(i < 10)
			z = 21;
		else
			z = 0;
		trees[i] = new Tree(random(100)+400+((i%10)*100),400,0.04,z);
	}
	horseman = new Horseman(1300,400-34);
	smooth();
	timer = millis();
}

void draw(){
	delta = millis() - timer;
	timer = millis();

	background(255);

	for(int i=0;i < trees.length/2; i++){
		trees[i].draw(delta);
	}
	noStroke();
	fill(90,90,90);
	quad(400,400,1400,400,1400-10,400-21,400+10,400-21);
	horseman.draw(delta);
	for(int i=trees.length/2;i < trees.length; i++){
		trees[i].draw(delta);
	}
}
